zooArrays_str;
zooStrUtils;
zooUtils;


global proc p4() {
	return;
	}


//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// SUBMIT UI - the submit UI is a moderately complicated UI, with template saving/loading functionality, and various checks and balances
//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@


	//normally UI procs are banned from utils scripts, but this is an exception because
	//having a UI for description input is so essential to any tools using perforce
	//this proc simply opens a window for description authoring, and has a submit and a
	//cancel button
	global proc p4submitUI( string $files[] ) {
		string $windowName = "p4SubmitUI";
		string $windowTitle = "submit comments";
		string $fileArrayStr = "{ \""+ `zooArrayToStr_str $files "\",\""` +"\" }";
		int $isEdit = `isEdit $files[0] {} {}`;

		for( $n=0; $n<`size $files`; $n++ ) $files[$n] = `zooResolvePath $files[$n]`;
		if( `window -exists $windowName` ) deleteUI $windowName;
		window -t $windowTitle -mb 1 -rtf 0 -maximizeButton 0 -sizeable 1 -width 600 -height 450 $windowName;
			string $form = `formLayout`;
				string $pane = `paneLayout -cn "horizontal2" -ps 1 100 40 p4SubmitPane`;
					string $text = `scrollField -tx "" -ww 1`;
					string $listForm = `formLayout`;
						string $fileLbl = `text -l "files being submitted - right click to list all files open for edit options (this is slow)"`;
						string $fileUI = `textScrollList -ams 1`;
					setParent ..;
				setParent ..;
				string $save = `button -en $isEdit -l(size($files)?"submit file":"submit files") -c( "p4submitUIAction "+ $fileUI +" "+ $text +";" )`;
				string $canc = `button -l "cancel" -c( "deleteUI "+ $windowName )`;
			menu -p $windowName -l "Comment Templates" -pmc( "p4CommentTemplateMenu p4TemplateMenu "+ $text ) p4TemplateMenu;

		for( $file in $files ) {
			textScrollList -e -a(tolower($file)) $fileUI;
			textScrollList -e -si(tolower($file)) $fileUI;
			}
		popupMenu -b 3 -p $fileUI;
			menuItem -l "list all files opened for edit" -c( "p4submitUIListAll "+ $fileUI );

		formLayout -e
			-af $fileLbl "top" 3
			-af $fileLbl "left" 0

			-ac $fileUI "top" 3 $fileLbl
			-af $fileUI "left" 0
			-af $fileUI "right" 0
			-af $fileUI "bottom" 0
			$listForm;

		formLayout -e
			-af $pane "top" 2
			-af $pane "left" 2
			-af $pane "right" 2
			-ac $pane "bottom" 2 $save

			-af $save "left" 2
			-ap $save "right" 1 50
			-af $save "bottom" 2

			-ap $canc "left" 1 50
			-af $canc "right" 2
			-af $canc "bottom" 2
			$form;

		showWindow $windowName;
		}


	global proc p4submitUIListAll( string $fileUI ) {
		string $opened[] = `p4_openedClient`;
		string $files[] = `textScrollList -q -si $fileUI`;

		textScrollList -e -ra $fileUI;
		for( $file in $opened ) textScrollList -e -a(tolower($file)) $fileUI;
		for( $file in $files ) textScrollList -e -si(tolower($file)) $fileUI;
		}


	global proc p4CommentTemplateMenu( string $parent, string $commentUI ) {
		setParent -m $parent;
		menu -e -dai $parent;

		string $dirL = `zooGetPresetDirectory "local" p4`;
		string $dirG = `zooGetPresetDirectory "global" p4`;
		string $templatesL[] = `zooListPresets "local" p4 ""`;
		string $templatesG[] = `zooListPresets "global" p4 ""`;

		for( $n=0; $n<`size $templatesL`; $n++ ) menuItem -l( `match "^[^\.]+" $templatesL[$n]` ) -c( "scrollField -e -tx(zooArrayToStr_str(`zooReadFile \""+ $dirL + $templatesL[$n] +"\" \"\"`,\"\")) \""+ $commentUI +"\";" );
		for( $n=0; $n<`size $templatesG`; $n++ ) menuItem -l( `match "^[^\.]+" $templatesG[$n]` ) -c( "scrollField -e -tx(zooArrayToStr_str(`zooReadFile \""+ $dirG + $templatesG[$n] +"\" \"\"`,\"\")) \""+ $commentUI +"\";" );
		menuItem -d 1;
		menuItem -l "Save Current as Template" -c( "{string $name = `promptDialog -t \"template name\" -m \"template name:\" -b OK -b cancel -db OK`; if( $name == \"OK\" ) zooSavePreset \"local\" p4 \"\" (`promptDialog -q -tx`) (`scrollField -q -tx "+ $commentUI +"`);}" );
		menuItem -d 1;
		menuItem -l "Preset Manager" -c( "zooPresetManager; zooPresetManagerWin \"local\" p4 \"\";" );
		}


	global proc p4submitUIAction( string $filesTSL, string $scrollField ) {
		string $files[] = `textScrollList -q -si $filesTSL`;
		string $comment = zooReplaceNewlines(`scrollField -q -tx $scrollField`,"\\n\\t");
		string $cur = `file -q -sn`;
		int $isCurFileInList = 0;

		if( $cur != "" ) {
			$cur = `tolower $cur`;
			for( $n=0; $n<`size $files`; $n++ ) $files[$n] = `tolower $files[$n]`;
			$isCurFileInList = `zooGetIdxOfElement_str $files $cur`;
			$isCurFileInList = $isCurFileInList<0? 0: 1;  //if its a negative number, its not in the list
			}

		//if the current file is in the list of files being submitted, check to see
		//if its been saved before submitting if it hasn't been saved, throw up a
		//warning and ask the user if they want the file to be saved or not.  this
		//is just a sanity check to make sure people don't submit unsaved scene files
		if( $isCurFileInList ) if( `file -q -modified` ) {
			string $ans = `confirmDialog -t "scene not saved" -m "seems your scene has been modified since last save\n\ndo you want me to save it before submitting?" -b "yes please" -b "no thanks" -b "cancel"`;
			if( $ans == "cancel" ) return;
			if( $ans == "yes please" ) {
				print "####  saving file before submitting!\n";
				file -f -save;
				}
			}
		p4_submit $files $comment;
		deleteUI p4SubmitUI;
		}


//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// COMMON FUNCTIONALITY - these are commonly used functions that aren't single line functions with the p4 wrappers
//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@


	//returns the perforce revision string for the given file - if the file arg
	//given is empty, then it assumes the currently opened file
	global proc string p4getRevStr( string $file ) {
		if( $file == "" ) $file = `file -q -sn`;

		//query perforce data about the current file
		string $dataNames[] = {};
		string $data[] = `p4_fstat $file $dataNames {} 1`;
		string $revStr = "x/x";
		int $have = `zooGetIdxOfElement_str $dataNames haveRev`;
		int $head = `zooGetIdxOfElement_str $dataNames headRev`;

		if( $head >= 0 ) $revStr = "/"+ $data[$head];
		if( $have >= 0 ) $revStr = $data[$have] + $revStr;
		else $revStr = "x"+ $revStr;

		return $revStr;
		}


	global proc string[] p4_openedClient() {
		string $files[] = `p4_opened`;
		string $filepaths[] = {};

		for( $n=0; $n<`size $files`; $n++ ) $filepaths[$n] = `clientPath $files[$n]`;

		return $filepaths;
		}


//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// DIRECT PERFORCE WRAPPERS - these are pretty much direct wrappers to the basic perforce command line tools
// where possible the commands are named as if they were p4 commandline commands, but with the first space
// being replaced by an underscore:  ie p4 add //repo/somefile/yay.txt becomes: p4_add //repo/somefile/yay.txt
//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@


	//adds a given file to perforce - returns the success of the command
	global proc int p4_add( string $files[] ) {
		string $fileStr = "";
		int $num = `size $files`;

		for( $n=0; $n<$num; $n++ ) {
			$files[$n] = `zooResolvePath $files[$n]`;
			$files[$n] = `zooReplaceChars $files[$n] "/" "\\"`;
			$fileStr += "\""+ $files[$n] +"\" ";
			}

		string $p4Cmd = "p4 add "+ $fileStr;
		print( $p4Cmd +"\n" );
		print `system $p4Cmd`;

		$p4Cmd = strip($p4Cmd);
		if( `match "opened for add$" $p4Cmd` != "" ) return 1;
		return 0;
		}


	//returns whether the file was successfully opened for edit or not
	global proc int p4_edit( string $files[] ) {
		string $fileStr = "";
		int $num = `size $files`;

		for( $n=0; $n<$num; $n++ ) {
			$files[$n] = `zooResolvePath $files[$n]`;
			$files[$n] = `zooReplaceChars $files[$n] "/" "\\"`;
			$fileStr += "\""+ $files[$n] +"\" ";
			}

		string $p4Cmd = "p4 edit "+ $fileStr;
		print( $p4Cmd +"\n" );
		print `system $p4Cmd`;

		$p4Cmd = strip($p4Cmd);
		if( `match "currently opened for edit$" $p4Cmd` != "" ) return 1;
		if( `match "opened for edit$" $p4Cmd` != "" ) return 1;
		return 0;
		}


	//syncs to a given revision number for all files - if the number is zero or negative,
	//then its relative to the head revision.  so p4_syncRev {} -2; does a sync to the
	//second last revision for all files, and 0 is the head revision
	global proc p4_sync( string $files[], int $rev ) {
		string $fileStr = "";
		int $num = `size $files`;

		for( $n=0; $n<$num; $n++ ) {
			$files[$n] = `zooResolvePath $files[$n]`;
			$files[$n] = `zooReplaceChars $files[$n] "/" "\\"`;
			if( $rev==0 ) $fileStr += "\""+ $files[$n] +"\" ";
			else if( $rev<0 ) {
				string $dataNames[] = {};
				string $data[] = `p4_fstat $files[$n] $dataNames {} 1`;
				int $headIdx = `zooGetIdxOfElement_str $dataNames headRev`;

				if( $headIdx != -1 ) $rev = ((int)$dataNames[$headIdx]) + $rev;
				if( $rev<=0 ) $rev = 1;  //can't have a negative or zero revision, so crop to 1
				$fileStr += "\""+ $files[$n] +"#"+ $rev +"\" ";
				}
			else $fileStr += "\""+ $files[$n] +"#"+ $rev +"\" ";
			}

		string $p4Cmd = "p4 sync "+ $fileStr;
		print( $p4Cmd +"\n" );
		print `system $p4Cmd`;
		}


	//deals with submitting a change, filling out the changelist file with appropriate
	//data, and publishing the changelist to perforce - returns the success state
	global proc int p4_submit( string $files[], string $comment ) {
		int $num = `size $files`;
		for( $n=0; $n<$num; $n++ ) $files[$n] = `zooResolvePath $files[$n]`;

		int $oneIsAlreadyOpen = 0;
		for( $n=0; $n<`size $files`; $n++ ) {
			string $dataNames[] = {};
			string $data[] = `p4_fstat $files[$n] $dataNames {} 1`;
			int $openIdx = `zooGetIdxOfElement_str $dataNames otherOpen`;

			if( $openIdx > 0 ) if( $data[$openIdx] == "1" ) {
				warning( $files[$n] +" seems to be open by someone already - please do the submit via the perforce interface" );
				$oneIsAlreadyOpen++;
				}
			}

		if( $oneIsAlreadyOpen ) return 0;

		string $user = `getenv USER`;
		string $p4User = `p4User`;
		string $computer = `getenv COMPUTERNAME`;
		string $tempPath = zooResolvePath(`getenv TEMP`+"/");
		string $tempFile = $tempPath +"p4_changelist_from_maya.txt";
		string $tempDirNative = `zooReplaceChars $tempFile "/" "\\"`;
		string $changelist = "Change:\tnew\n\nClient:\t"+ $computer +"\n\nUser:\t"+ $p4User +"\n\nStatus:\tnew\n\nDescription:\n\t"+ $comment +"\n\nFiles:";
		int $fileID = `fopen $tempFile "w"`;

		for( $n=0; $n<$num; $n++ ) {
			string $tempPath = `p4Path $files[$n]`;
			$changelist += "\n\t"+ $tempPath +"\t# edit";
			}

		fprint $fileID $changelist;
		fclose $fileID;
		print $changelist;
		string $p4Cmd = `system( "p4 submit -i < \""+ $tempDirNative +"\"" )`;
		print $p4Cmd;
		$p4Cmd = strip($p4Cmd);
		sysFile -delete $tempFile;
		if( `match "Change [0-9]+ submitted." $p4Cmd` != "" ) return 1;

		return 0;
		}


	global proc int p4_revert( string $files[] ) {
		string $fileStr = "";
		int $num = `size $files`;

		for( $n=0; $n<$num; $n++ ) {
			$files[$n] = `zooResolvePath $files[$n]`;
			$files[$n] = `zooReplaceChars $files[$n] "/" "\\"`;
			$fileStr += "\""+ $files[$n] +"\" ";
			}

		string $p4Cmd = "p4 revert "+ $fileStr;
		print( $p4Cmd +"\n" );
		print `system $p4Cmd`;

		return 1;
		}


	global proc string[] p4_opened() {
		string $p4Cmd = `system( "p4 opened" )`;
		string $outputToks[] = `zooTokenize (strip($p4Cmd)) "\n"`;
		string $opened[] = {};

		for( $n=0; $n<`size $outputToks`; $n++ ) $opened[$n] = `match "^[^#]+" (strip($outputToks[$n]))`;

		return $opened;
		}


	//returns the p4 stats for the file in question.  the two arrays get filled with
	//the field names, and the corresponding data for those fields, and the data gets
	//returned - for convenience
	global proc string[] p4_fstat( string $file, string $dataNames[], string $data[], int $resolvePath ) {
		clear $dataNames;
		clear $data;

		string $nativeFile = $file;
		if( $resolvePath ) {
			$file = `zooResolvePath $file`;
			$nativeFile = `zooReplaceChars $file "/" "\\"`;
			}
		string $p4Cmd = `system( "p4 fstat \""+ $nativeFile +"\"" )`;
		string $outputToks[] = `zooTokenize (strip($p4Cmd)) "\n"`;

		for( $n=0; $n<`size $outputToks`; $n++ ) {
			$outputToks[$n] = `strip $outputToks[$n]`;
			string $pfxToStrip = `match "^[\. ]+" $outputToks[$n]`;
			$outputToks[$n] = `substitute ( "^"+ $pfxToStrip ) $outputToks[$n] ""`;
			string $temp[] = `zooTokenize $outputToks[$n] " "`;  //strip is needed because windows uses the \r\n line returns, not just \n
			$dataNames[$n] = $temp[0];
			$data[$n] = zooArrayToStr_str(`zooIndexCrop_str $temp "1:"`," ");
			}

		return $data;
		}


//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@
// SUPPORT PROCS - these are convenience procs to simplify the wrapper commands
//@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@@


	//returns an int as to whether the current file is synced to the latest perforce
	//revision.  returns -1 if file is not in perforce.  the dataNames and data args
	//are optional.  if present, they get filled with the fstat results, so they can
	//be reused without re-querying
	global proc int isLatest( string $file, string $dataNames[], string $data[] ) {
		$data = `p4_fstat $file $dataNames {} 1`;
		int $headRev = `zooGetIdxOfElement_str $dataNames "headRev"`;
		int $haveRev = `zooGetIdxOfElement_str $dataNames "haveRev"`;
		int $actIdx = `zooGetIdxOfElement_str $dataNames "action"`;

		//so if the head rev is -1 that means it doesn't have a head revision which
		//means either the file isn't in perforce - OR its been added to perforce but
		//the add hasn't been submitted yet - so check for that.  if the file is
		//waiting for add submission, then return 0 instead of -1
		if( $headRev == -1 ) {
			if( $actIdx == -1 ) return -1;
			else if( $data[$actIdx] == "add" ) return 0;
			}
		if( $haveRev == -1 ) return 0;
		if( $data[$headRev] == $data[$haveRev] ) return 1;
		return 0;
		}


	//returns whether a given file is already open for edit or not
	global proc int isEdit( string $file, string $dataNames[], string $data[] ) {
		$data = `p4_fstat $file $dataNames {} 1`;
		int $isEdit = 0;
		int $actIdx = `zooGetIdxOfElement_str $dataNames "action"`;

		if( $actIdx != -1 ) {
			if( $data[$actIdx] == "edit" ) $isEdit = 1;
			else if( $data[$actIdx] == "add" ) $isEdit = 1;
			}
		if( `size $data` < 3 ) $isEdit = -1;  //indicates that the file isn't in perforce

		return $isEdit;
		}


	//given a filepath, this proc returns a path relative to the perforce
	//repository - some p4 commands use repository relative paths...
	global proc string p4Path( string $filepath ) {
		string $names[] = {};
		string $data[] = `p4_fstat $filepath $names {} 1`;
		int $base = `zooGetIdxOfElement_str $names "depotFile"`;

		if( $base == -1 ) return $filepath;

		return $data[$base];
		}


	//given a filepath, this proc returns a path relative to the perforce
	//repository - some p4 commands use repository relative paths...
	global proc string clientPath( string $filepath ) {
		string $names[] = {};
		string $data[] = `p4_fstat $filepath $names {} 0`;
		int $base = `zooGetIdxOfElement_str $names "clientFile"`;

		if( $base == -1 ) return $filepath;

		$data[$base] = `substituteAllString $data[$base] "\\" "/"`;
		return $data[$base];
		}


	//$user can be blank - uses current user if so
	//should probably query the username the proper way - by doing a "p4 user -o" and parsing the output
	global proc string p4User() {
		string $info = tolower( `system "p4 info"` );
		string $data[] = `zooTokenize $info "\n"`;
		string $line = "";
		int $num = `size $data`;

		for( $n=0; $n<$num; $n++ ) if( `match "^user name: " $data[$n]` != "" ) {
			$line = `strip $data[$n]`;
			break;
			}

		$line = `match "[^ :]+$" $line`;

		return $line;
		}


